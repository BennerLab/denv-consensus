import pandas

sample_df = pandas.read_table("sample_sheet.txt", index_col="sample")

def get_subtype_reference(wildcards):
    paf_df = pandas.read_table(f"megahit/{wildcards.sample}/contigs.paf", header=None)

    # find the most frequent contig match
    main_hit = paf_df.value_counts(5).index[0] # column index 5 is the reference sequence name

    return f"reference/{main_hit}.fasta"

rule all:
    input:
        expand("consensus/{sample}.fasta", sample=sample_df.index)

rule assembly:
    input:
        r1=lambda x: "reads/" + sample_df.loc[x.sample, "read1"],
        r2=lambda x: "reads/" + sample_df.loc[x.sample, "read2"]
    output:
        "megahit/{sample}/final.contigs.fa"
    threads: 1 # currently fails on Mac if more than one thread is used
    shell:
        """
        rm -rf megahit/{wildcards.sample}
        megahit --num-cpu-threads {threads} -1 {input.r1} -2 {input.r2} -o megahit/{wildcards.sample} 2> /dev/null
        """

rule align_contigs:
    input:
        "megahit/{sample}/final.contigs.fa"
    output:
        "megahit/{sample}/contigs.paf"
    params:
        reference="reference/NCBI.fasta"
    log:
        "megahit/{sample}.log"
    threads: 3
    shell:
        "minimap2 -t {threads} {params.reference} {input} > {output} 2> {log}"

rule align_reads:
    input:
        r1=lambda x: "reads/" + sample_df.loc[x.sample, "read1"],
        r2=lambda x: "reads/" + sample_df.loc[x.sample, "read2"],
        consensus="megahit/{sample}/contigs.paf"
    output:
        "align/{sample}.bam"
    params:
        reference=get_subtype_reference
        # function to read PAF and return correct reference
    log:
        "align/{sample}.log"
    threads: 3
    shell:
        "minimap2 -a -t {threads} {params.reference} {input} 2> {log} | samtools view -u -F 4 | samtools sort -o {output}"

rule generate_consensus:
    input:
        alignments="align/{sample}.bam",
        consensus="megahit/{sample}/contigs.paf"
    output:
        vcf="consensus/{sample}.vcf.gz",
        fasta="consensus/{sample}.fasta"
    params:
        reference=get_subtype_reference
    shell:
        """
        bcftools mpileup -E -Ou -f {params.reference} {input.alignments} |bcftools call -mv -Oz -o {output.vcf}
        tabix {output.vcf}
        bcftools consensus {output.vcf} -f {params.reference} -o {output.fasta}
        """
